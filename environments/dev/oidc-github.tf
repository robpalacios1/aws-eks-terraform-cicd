# ====================================================================
# 1. Create Identity Provider (IdP) of Github on AWS
# ====================================================================

resource "aws_iam_openid_connect_provider" "github_actions" {
  url = "https://token.actions.githubusercontent.com"
  client_id_list = [ "sts.amazonaws.com" ]
  thumbprint_list = [ 
    "1b511abead59c6ce207077c0bf0e0043b1382612", 
    "6938fd4d98bab03faadb97b34396831e3780aea1" 
  ]
}

# ====================================================================
# 2. Create Role of IAM that Github is going to assume
# ====================================================================

resource "aws_iam_role" "github_actions_role" {
  name = "github-actions-deploy-role"
  
  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
        {
            Effect = "Allow"
            Principal = {
                Federated = aws_iam_openid_connect_provider.github_actions.arn
            },
            Action = "sts:AssumeRoleWithWebIdentity",
            Condition = {
                "StringEquals" = {
                    "token.actions.githubusercontent.com:aud" = "sts.amazonaws.com"
                },
                "StringLike" = {
                    "token.actions.githubusercontent.com:sub" = "repo:robpalacios1/aws-eks-terraform-cicd:*"
                }
            }
        }
    ]
  })
}

# ====================================================================
# 3. Permission to Role
# ====================================================================

resource "aws_iam_role_policy_attachment" "github_actions_admin" {
  role = aws_iam_role.github_actions_role.name
  policy_arn = "arn:aws:iam::aws:policy/AdministratorAccess"
}

# ====================================================================
# 4. Print the role ARN for use it in Github
# ====================================================================

output "github_actions_role_arn" {
  description = "role for user in Github"
  value = aws_iam_role.github_actions_role.arn
}